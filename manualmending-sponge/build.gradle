buildscript {
    repositories {
        mavenCentral()
        maven {
            name = 'sponge'
            url = 'https://repo.spongepowered.org/maven'
        }
        maven {
            name = 'forge'
            url = 'http://files.minecraftforge.net/maven'
        }
    }

    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'gradle.plugin.org.spongepowered:spongegradle:0.8.1'
        classpath 'org.spongepowered:mixingradle:0.5-SNAPSHOT'
    }
}

plugins {
    id 'net.minecrell.vanillagradle.server' version '2.2-6'
    id 'org.spongepowered.plugin' version '0.8.1'
}

apply plugin: 'org.spongepowered.mixin'

group = pluginGroup
version = pluginVersion

minecraft {
    version = "1.12.2"
    mappings = "stable_39"
    makeObfSourceJar = false
}

configurations {
    shade
    compile.extendsFrom shade
}

dependencies {
    shade project(':manualmending-common')
    compile 'org.spongepowered:spongeapi:7.2.0'
    compile 'org.spongepowered:mixin:0.7.3-SNAPSHOT'
}

sponge.plugin.id = pluginId

sourceSets {
    main {
        ext.refMap = "mixins.manualmending.refmap.json"
    }
}

jar {
    manifest.attributes (
            "TweakClass": "org.spongepowered.asm.launch.MixinTweaker",
            "MixinConfigs": "mixins.manualmending.json",
            "FMLCorePluginContainsFMLMod": "true"
    )

    configurations.shade.each {
        from(it.isDirectory() ? it : project.zipTree(it)) {
            exclude "META-INF/**"
        }
    }
}
